 Alter PROCEDURE sp_LoginUser    
    @Username NVARCHAR(150),    
    @Password NVARCHAR(200),    
    @Result INT OUTPUT    
AS    
BEGIN    
    SET NOCOUNT ON;    
  
    DECLARE @HashHex NVARCHAR(128) = CONVERT(NVARCHAR(128), HASHBYTES('SHA2_512', @Password), 2);    
    DECLARE @LoginId INT;    
  
    SELECT @LoginId = LoginId    
    FROM UserLogins    
    WHERE Username = @Username AND PasswordHash = @HashHex AND IsActive = 1;    
  
    IF @LoginId IS NULL  
    BEGIN    
        SET @Result = -1; RETURN;    
    END    
  
    IF EXISTS(SELECT 1 FROM Users WHERE LoginId = @LoginId AND IsVerified = 0)    
    BEGIN    
        SET @Result = -2; RETURN;    
    END    
  
    SET @Result = 1;    
  
    SELECT     
        u.UserId,    
        ul.LoginId,
		bp.BusinessId,
        ul.Username,    
        u.FullName,    
        u.PrimaryEmail,    
        u.PrimaryPhone,    
        u.Role,    
        bp.BusinessName,    
        bp.BusinessType,    
        bp.GSTNumber,    
        bp.City,    
        bp.State,    
        bp.Pincode    
    FROM Users u    
    JOIN UserLogins ul ON u.LoginId = ul.LoginId    
    LEFT JOIN BusinessProfiles bp ON bp.UserId = u.UserId    
    WHERE ul.LoginId = @LoginId;    
END


GO


ALTER PROCEDURE dbo.proc_GetSellerDashboard  
    @BusinessId INT  
AS  
/*  
exec dbo.proc_GetSellerDashboard 2  
*/  
BEGIN  
    SET NOCOUNT ON;  
  
    ---------------------------------------------------------  
    -- 1) Seller / Business info  
    ---------------------------------------------------------  
    SELECT  
        b.BusinessId,  
        b.BusinessName,  
        b.BusinessType,  
        b.GSTNumber,  
        b.ContactPerson,  
        b.ContactPhone,  
        b.BusinessEmail,  
        b.Address,  
        b.City,  
        b.State,  
        b.Pincode,  
        b.IsVerified  
    FROM BusinessProfiles b  
    WHERE b.BusinessId = @BusinessId;  
  
    ---------------------------------------------------------  
    -- 2) Summary stats (New / Accepted / Shipped / Revenue)  
    ---------------------------------------------------------  
    SELECT  
        SUM(CASE WHEN o.Status = 'New' THEN 1 ELSE 0 END) AS NewOrders,  
        SUM(CASE WHEN o.Status = 'Accepted' THEN 1 ELSE 0 END) AS AcceptedOrders,  
        SUM(CASE WHEN o.Status = 'Shipped' THEN 1 ELSE 0 END) AS ShippedOrders,  
        ISNULL(SUM(o.TotalAmount), 0) AS Revenue  
    FROM OrdersInfo o  
    WHERE o.SellerBusinessId = @BusinessId  
      AND o.IsActive = 1;  
  
    ---------------------------------------------------------  
    -- 3) Recent orders (top 6, newest first) + item summary  
    ---------------------------------------------------------  
    SELECT TOP (6)  
        o.OrderId,  
        o.OrderNumber,  
        o.BuyerBusinessId,  
        bp.BusinessName AS BuyerName,  
        o.TotalAmount,  
        o.Status,  
        o.CreatedAt AS OrderDate,  
        (SELECT TOP (1) oi.ProductId FROM OrderItems oi WHERE oi.OrderId = o.OrderId FOR XML PATH('')) AS SampleProductId,  
        STUFF((  
            SELECT '; ' + CAST(oi.Quantity AS VARCHAR(10)) + 'x ' + p.ProductName  
            FROM OrderItems oi  
            LEFT JOIN Products p ON p.ProductId = oi.ProductId  
            WHERE oi.OrderId = o.OrderId  
            FOR XML PATH(''), TYPE  
        ).value('.', 'NVARCHAR(MAX)'), 1, 2, '') AS ItemsSummary  
    FROM OrdersInfo o  
    LEFT JOIN BusinessProfiles bp ON bp.BusinessId = o.BuyerBusinessId  
    WHERE o.SellerBusinessId = @BusinessId  
      AND o.IsActive = 1  
    ORDER BY o.CreatedAt DESC;  
  
    ---------------------------------------------------------  
    -- 4) Top products for this seller (by stock or recent)  
    ---------------------------------------------------------  
    SELECT TOP (6)  
        p.ProductId,  
        p.ProductCode,  
        p.ProductName,  
        p.PricePerGram,  
        p.MakingCharges,  
        p.Stock,  
        --pi.ImageUrl AS PrimaryImage,  
        pi.ImageUrl AS PrimaryImage  
    FROM Products p  
    LEFT JOIN ProductImages pi ON pi.ProductId = p.ProductId AND pi.IsPrimary = 1  
    WHERE p.BusinessId = @BusinessId  
      AND p.IsActive = 1  
    ORDER BY p.Stock DESC, p.CreatedAt DESC;  
  
    ---------------------------------------------------------  
    -- 5) Unread notifications count  
    ---------------------------------------------------------  
    SELECT  
        ISNULL(SUM(CASE WHEN n.IsRead = 0 THEN 1 ELSE 0 END), 0) AS UnreadNotifications  
    FROM Notifications n  
    WHERE n.BusinessId = @BusinessId;  
END  


GO

ALTER PROCEDURE dbo.sp_AddProductImage
    @ProductId INT,
    @BusinessId INT,
    @ImageUrl NVARCHAR(1000),
    @AltText NVARCHAR(500) = NULL,
    @IsPrimary BIT = 0,
    @SortOrder INT = 0,
    @OutResult INT OUTPUT,     -- 1=OK, -1=error
    @OutImageId INT OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    BEGIN TRY
        IF @IsPrimary = 1
        BEGIN
            UPDATE dbo.ProductImages
            SET IsPrimary = 0
            WHERE ProductId = @ProductId AND IsActive = 1;
        END

        INSERT INTO dbo.ProductImages (ProductId, ImageUrl, AltText, IsPrimary, SortOrder, CreatedAt, IsActive)
        VALUES (@ProductId, @ImageUrl, @AltText, @IsPrimary, @SortOrder, SYSUTCDATETIME(), 1);

        SET @OutImageId = CAST(SCOPE_IDENTITY() AS INT);
        SET @OutResult = 1;
    END TRY
    BEGIN CATCH
        SET @OutResult = -1;
        SET @OutImageId = 0;
    END CATCH
END
GO

Alter PROCEDURE dbo.proc_GetProductImages
    @ProductId INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        ImageId,
        ProductId,
        ImageUrl,
        AltText,
        IsPrimary,
        SortOrder,
        CreatedAt
    FROM ProductImages
    WHERE ProductId = @ProductId 
      AND IsActive = 1
    ORDER BY IsPrimary DESC, SortOrder ASC;
END

GO

-- soft delete
ALter PROCEDURE dbo.sp_DeleteProductImage
    @ImageId INT,
    @OutResult INT OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    BEGIN TRY
        UPDATE dbo.ProductImages SET IsActive = 0 WHERE ImageId = @ImageId;
        SET @OutResult = 1;
    END TRY
    BEGIN CATCH
        SET @OutResult = -1;
    END CATCH
END
GO

GO


